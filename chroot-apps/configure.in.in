m4_define([chrootapps_version_major], [2])
m4_define([chrootapps_version_minor], [0])
m4_define([chrootapps_version_build], [~~+svn@REVISION@])
#m4_define([chrootapps_version_build], [])
m4_define([chrootapps_version], [chrootapps_version_major().chrootapps_version_minor()chrootapps_version_build()])

AC_COPYRIGHT([Copyright (c) Gauvain Pocentek])
AC_INIT([ovd-chroot-apps], [chrootapps_version], [http://bugs.ulteo.com])
AC_PREREQ([2.50])

AM_INIT_AUTOMAKE([1.8])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()

AC_PROG_AWK()
AC_PROG_CC()
AC_PROG_LD()
AC_PROG_INSTALL()
AC_PROG_INTLTOOL()
AM_PROG_CC_C_O()
AC_C_CONST()

AC_HEADER_STDC()
AC_CHECK_HEADERS([stdlib.h string.h unistd.h sys/types.h])

AC_CHECK_FUNCS([])

XDT_I18N([fr])

XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.6.0])
XDT_CHECK_PACKAGE([LIBXFCEGUI4], [libxfcegui4-1.0], [4.4])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.4])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.4])
XDT_CHECK_PACKAGE([THUNARVFS], [thunar-vfs-1])

XDT_FEATURE_DEBUG()

AC_MSG_CHECKING([PLATFORM_CPPFLAGS])
AC_MSG_RESULT([$PLATFORM_CPPFLAGS])
AC_SUBST([PLATFORM_CPPFLAGS])
AC_MSG_CHECKING([PLATFORM_CFLAGS])
AC_MSG_RESULT([$PLATFORM_CFLAGS])
AC_SUBST([PLATFORM_CFLAGS])
AC_MSG_CHECKING([PLATFORM_LDFLAGS])
AC_MSG_RESULT([$PLATFORM_LDFLAGS])
AC_SUBST([PLATFORM_LDFLAGS])

# Check for python (needed by cups2all and d-f2xml)
AM_PATH_PYTHON

# Check for cups if cups2all is enabled
AC_ARG_ENABLE([cups2all],
              AC_HELP_STRING([--enable-cups2all],
                             [install the cups2all driver (default: yes)]),
              [enable_cups2all=$enableval],
              [enable_cups2all=yes])
if test x"$enable_cups2all" = x"yes"; then
    AC_PATH_PROG(CUPS_CONFIG, cups-config)
    if test "x$CUPS_CONFIG" = x; then
        AC_MSG_ERROR(You need to install cups-config.)
    fi
    CUPSSERVERBIN=`$CUPS_CONFIG --serverbin`
    CUPSSERVERROOT=`$CUPS_CONFIG --serverroot`
    CUPSDATADIR=`$CUPS_CONFIG --datadir`
    AC_SUBST(CUPSSERVERBIN)
    AC_SUBST(CUPSSERVERROOT)
    AC_SUBST(CUPSDATADIR)
fi
AM_CONDITIONAL(CUPS2ALL, test x"$enable_cups2all" = x"yes")

# startovd default start script
AC_ARG_WITH(
    startovd,
    AC_HELP_STRING([--with-startovd=xfce|kde], [select which startup script will be used (default=xfce)]),
    [ case "$withval" in
        xfce|kde)
            startovd="startovd-$withval";;
        *)
            AC_MSG_ERROR(Wrong value $withval, use ./configure --help to find valid options);;
      esac
      STARTOVD=$startovd
    ],
    [STARTOVD=startovd-xfce]
)
AC_SUBST(STARTOVD)

AC_OUTPUT([
Makefile
restricted-menu/Makefile
lmessage/Makefile
po/Makefile.in
cups2all/Makefile
desktopfiles2xml/Makefile
start-ovd/Makefile
])

echo
echo "Configuration:"
echo "* cups2all: $enable_cups2all"
echo "* startovd: $STARTOVD"

