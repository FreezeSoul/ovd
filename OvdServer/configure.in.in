m4_define([uovdserver_version_major], [99])
m4_define([uovdserver_version_minor], [99])
m4_define([uovdserver_version_build], [~trunk+svn@REVISION@])
m4_define([uovdserver_version], [uovdserver_version_major().uovdserver_version_minor()uovdserver_version_build()])

AC_INIT([ovdserver], [uovdserver_version], [http://bugs.ulteo.com])

# Init automake
AM_INIT_AUTOMAKE([1.9 tar-pax])

# Checks for programs.
AC_PROG_INSTALL

# Intl (borrowed from intltool.m4)
AC_PATH_PROG(INTLTOOL_UPDATE, [intltool-update])
AC_PATH_PROG(INTLTOOL_MERGE, [intltool-merge])
AC_PATH_PROG(INTLTOOL_EXTRACT, [intltool-extract])
if test -z "$INTLTOOL_UPDATE" -o -z "$INTLTOOL_MERGE" -o -z "$INTLTOOL_EXTRACT"; then
    AC_MSG_ERROR([The intltool scripts were not found. Please install intltool.])
fi

# Check the gettext tools to make sure they are GNU
AC_PATH_PROG(XGETTEXT, xgettext)
AC_PATH_PROG(MSGMERGE, msgmerge)
AC_PATH_PROG(MSGFMT, msgfmt)
if test -z "$XGETTEXT" -o -z "$MSGMERGE" -o -z "$MSGFMT"; then
    AC_MSG_ERROR([GNU gettext tools not found; required for intltool])
fi
xgversion="`$XGETTEXT --version|grep '(GNU ' 2> /dev/null`"
mmversion="`$MSGMERGE --version|grep '(GNU ' 2> /dev/null`"
mfversion="`$MSGFMT --version|grep '(GNU ' 2> /dev/null`"
if test -z "$xgversion" -o -z "$mmversion" -o -z "$mfversion"; then
    AC_MSG_ERROR([GNU gettext tools not found; required for intltool])
fi

AC_PATH_PROG(INTLTOOL_PERL, [perl])
if test -z "$INTLTOOL_PERL"; then
   AC_MSG_ERROR([perl not found; required for intltool])
fi
if test -z "`$INTLTOOL_PERL -v | fgrep '5.' 2> /dev/null`"; then
   AC_MSG_ERROR([perl 5.x required for intltool])
fi
if test "x$2" != "xno-xml"; then
   AC_MSG_CHECKING([for XML::Parser])
   if `$INTLTOOL_PERL -e "require XML::Parser" 2>/dev/null`; then
       AC_MSG_RESULT([ok])
   else
       AC_MSG_ERROR([XML::Parser perl module is required for intltool])
   fi
fi

AC_CONFIG_FILES([
Makefile
config/Makefile
ovd/Makefile
ovd/Communication/Makefile
ovd/Platform/Makefile
ovd/Platform/Windows/Makefile
ovd/Platform/Linux/Makefile
ovd/Role/Makefile
ovd/Role/ApplicationServer/Makefile
ovd/Role/FileServer/Makefile
])
AC_OUTPUT
