AC_INIT([ovd-session-manager], [@VERSION@], [http://bugs.ulteo.com])

# Init automake
AM_INIT_AUTOMAKE([1.9 tar-pax])

# Checks for programs.
AC_PROG_INSTALL

# Intl (borrowed from intltool.m4)
AC_PATH_PROG(INTLTOOL_UPDATE, [intltool-update])
AC_PATH_PROG(INTLTOOL_MERGE, [intltool-merge])
AC_PATH_PROG(INTLTOOL_EXTRACT, [intltool-extract])
if test -z "$INTLTOOL_UPDATE" -o -z "$INTLTOOL_MERGE" -o -z "$INTLTOOL_EXTRACT"; then
    AC_MSG_ERROR([The intltool scripts were not found. Please install intltool.])
fi

# Check the gettext tools to make sure they are GNU
AC_PATH_PROG(XGETTEXT, xgettext)
AC_PATH_PROG(MSGMERGE, msgmerge)
AC_PATH_PROG(MSGFMT, msgfmt)
if test -z "$XGETTEXT" -o -z "$MSGMERGE" -o -z "$MSGFMT"; then
    AC_MSG_ERROR([GNU gettext tools not found; required for intltool])
fi
xgversion="`$XGETTEXT --version|grep '(GNU ' 2> /dev/null`"
mmversion="`$MSGMERGE --version|grep '(GNU ' 2> /dev/null`"
mfversion="`$MSGFMT --version|grep '(GNU ' 2> /dev/null`"
if test -z "$xgversion" -o -z "$mmversion" -o -z "$mfversion"; then
    AC_MSG_ERROR([GNU gettext tools not found; required for intltool])
fi

AC_PATH_PROG(INTLTOOL_PERL, [perl])
if test -z "$INTLTOOL_PERL"; then
   AC_MSG_ERROR([perl not found; required for intltool])
fi
if test -z "`$INTLTOOL_PERL -v | fgrep '5.' 2> /dev/null`"; then
   AC_MSG_ERROR([perl 5.x required for intltool])
fi
if test "x$2" != "xno-xml"; then
   AC_MSG_CHECKING([for XML::Parser])
   if `$INTLTOOL_PERL -e "require XML::Parser" 2>/dev/null`; then
       AC_MSG_RESULT([ok])
   else
       AC_MSG_ERROR([XML::Parser perl module is required for intltool])
   fi
fi
GETTEXT_PACKAGE=uovdsm
AC_SUBST(GETTEXT_PACKAGE)
ALL_LINGUAS=$(sed -r -e "/^#/d" -e "s/#.*//" "po/LINGUAS")
AC_SUBST(ALL_LINGUAS)

# Allow use of already installed javascript libraries
AC_ARG_WITH([prototypedir],
    [AC_HELP_STRING([--with-prototypedir],
        [use a local installation of the Prototype javascript library])],
    ,
    [with_prototypedir=no])
if test x"$with_prototypedir" != xno; then
    if test -f "$with_prototypedir/prototype.js"; then
        PROTOTYPEDIR="$with_prototypedir"
        AC_SUBST(PROTOTYPEDIR)
        echo "Using $PROTOTYPEDIR for Prototype library."
    else
        AC_MSG_WARN([$with_prototypedir/prototype.js doesn't exist; using builtin Prototype library.])
        PROTOTYPEDIR=
    fi
fi
AM_CONDITIONAL(PROTOTYPE, [test -n "$PROTOTYPEDIR"])

AC_ARG_WITH([scriptaculousdir],
    [AC_HELP_STRING([--with-scriptaculousdir],
        [use a local installation of the Scriptaculous javascript library])],
    ,
    [with_scriptaculousdir=no])
if test x"$with_scriptaculousdir" != xno; then
    if test -f "$with_scriptaculousdir/scriptaculous.js"; then
        SCRIPTACULOUSDIR="$with_scriptaculousdir"
        AC_SUBST(SCRIPTACULOUSDIR)
        echo "Using $SCRIPTACULOUSDIR for Scriptaculous library."
    else
        AC_MSG_WARN([$with_scriptaculousdir/scriptaculous.js doesn't exist; using builtin Prototype library.])
        SCRIPTACULOUSDIR=
    fi
fi
AM_CONDITIONAL(SCRIPTACULOUS, [test -n "$SCRIPTACULOUSDIR"])

AC_ARG_WITH([libchartdir],
    [AC_HELP_STRING([--with-libchartdir],
        [use a local installation of the libchart PHP library])],
    ,
    [with_libchartdir=no])
if test x"$with_libchartdir" != xno; then
    if test -f "$with_libchartdir/classes/libchart.php"; then
        LIBCHARTDIR="$with_libchartdir"
        AC_SUBST(LIBCHARTDIR)
        echo "Using $LIBCHARTDIR for libchart library."
    else
        AC_MSG_WARN([$with_libchartdir/classes/libchart.php doesn't exist; charts on reports won't be available.])
        LIBCHARTDIR=
    fi
fi
AM_CONDITIONAL(LIBCHART, [test -n "$LIBCHARTDIR"])

AC_ARG_WITH([ulteo-applet],
    [AC_HELP_STRING([--with-ulteo-applet],
        [where is installed the ulteo OVD applet])],
    ,
    [with_ulteo_applet=])
if test x"$with_ulteo_applet" = x""; then
    for p in /usr /usr/local /opt; do
        if test -e $p/share/ulteo/applets/ulteo-applet.jar; then
            with_ulteo_applet=$p/share/ulteo/applets/ulteo-applet.jar
        fi
    done
fi
if test x"$with_ulteo_applet" = x""; then
    AC_MSG_ERROR([No ulteo applet found. Install it first or specify its path with the --with-ulteo-applet option])
else
    ULTEOAPPLET="$with_ulteo_applet"
fi
AC_SUBST(ULTEOAPPLET)

AC_ARG_WITH([check-applet],
    [AC_HELP_STRING([--with-check-applet],
        [where is installed the java test applet])],
    ,
    [with_check_applet=])
if test x"$with_check_applet" = x""; then
    for p in /usr /usr/local /opt; do
        if test -e $p/share/ulteo/applets/CheckJava.jar; then
            with_check_applet=$p/share/ulteo/applets/CheckJava.jar
        fi
    done
fi
if test x"$with_check_applet" = x""; then
    AC_MSG_ERROR([No java test applet found. Install it first or specify its path with the --with-check-applet option])
else
    CHECKAPPLET="$with_check_applet"
fi
AC_SUBST(CHECKAPPLET)


AC_CONFIG_FILES([
Makefile
po/Makefile
config/Makefile
scripts/Makefile
web/Makefile
web/client/Makefile
web/webservices/admin/Makefile
web/webservices/Makefile
web/plugins/Makefile
web/plugins/FS/Makefile
web/modules/Makefile
web/modules/AuthMethod/Makefile
web/modules/UserGroupDB/Makefile
web/modules/UserDB/Makefile
web/modules/ApplicationDB/Makefile
web/modules/ApplicationsGroupDB/Makefile
web/admin/ajax/Makefile
web/admin/init/Makefile
web/admin/media/script/Makefile
web/admin/media/script/ajax/Makefile
web/admin/media/script/lib/calendarpopup/Makefile
web/admin/media/script/lib/nifty/Makefile
web/admin/media/script/lib/prototype/Makefile
web/admin/media/script/lib/scriptaculous/Makefile
web/admin/media/script/lib/Makefile
web/admin/media/style/Makefile
web/admin/media/image/loadbalancing/Makefile
web/admin/media/image/menu/Makefile
web/admin/media/image/Makefile
web/admin/media/Makefile
web/admin/includes/Makefile
web/admin/classes/Makefile
web/admin/Makefile
web/includes/Makefile
web/includes/functions.inc.d/Makefile
web/classes/Makefile
web/classes/abstract/Makefile
web/classes/abstract/liaison/Makefile
web/classes/configelements/Makefile
web/classes/events/Makefile
web/classes/tasks/Makefile
web/PEAR/Makefile
web/PEAR/Auth/Makefile
web/PEAR/Auth/SASL/Makefile
web/PEAR/CAS/Makefile
web/PEAR/CAS/PGTStorage/Makefile
web/PEAR/CAS/languages/Makefile
web/PEAR/Mail/Makefile
web/PEAR/Net/Makefile
web/PEAR/HTTP/Makefile
web/PEAR/HTTP/WebDAV/Makefile
web/PEAR/HTTP/WebDAV/Server/Makefile
web/PEAR/HTTP/WebDAV/Tools/Makefile
web/events/Makefile
web/events/callbacks/Makefile
web/events/callbacks/SessionStart/Makefile
web/events/callbacks/SessionStatusChanged/Makefile
web/events/callbacks/ServerStatusChanged/Makefile
web/events/callbacks/SqlFailure/Makefile
examples/Makefile
extra/Makefile
])
AC_OUTPUT
